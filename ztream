#! /bin/zsh

PLAYER='mpv'
PLAYER_OPTS='--no-video'
LAST_STATION_PATH="$HOME/.last_station"
FIFO=1
FIFO_PATH="$HOME/.ztream_fifo"

typeset -A stations
stations=('beatblender' http://sfstream1.somafm.com:8384
          'cliq hop' http://mp2.somafm.com:2668
          'drone zone' http://mp4.somafm.com:80
          'Def Con' http://sfstream1.somafm.com:6200
          'drum n bass' http://nrgetik.streams.dnbradio.at.ilovedrumnbass.com:8000/
          'dubstep beyond' http://mp1.somafm.com:8400
          'groove salad' http://voxsc1.somafm.com:8032
          'sonic universe' http://mp2.somafm.com:8604
          'YT: majesticcasual' '--playlist=majesticcasual'

          # This works with a list of files or (with mpv) youtube links; one per line
          # 'test ' '--playlist=/home/bryan/dotfiles/bin/test'
)

###############################################################################
# Unless you have an idea of what you're doing, there's no need to head below.
###############################################################################

function get_action() {
    actions=('change' 'random' 'last' 'stop')
    if [ ${actions[(r)$1]} ]; then 
        action=$1
    else
        action=$(print -r -l ${actions} | dmenu)
    fi
}

function play_station() {
    pkill $PLAYER 
    station=$1; 
    echo "Playing $station..."
    echo $station >! $LAST_STATION_PATH
    if [ $FIFO ]; then
        if [ ! -p $FIFO_PATH ]; then
            mkfifo "$FIFO_PATH"
            exit
        fi
        $PLAYER $PLAYER_OPTS --input-file="$FIFO_PATH" $stations[$station] 1>&2 2>/dev/null &
    else
        $PLAYER $PLAYER_OPTS $stations[$station] 1>&2 2>/dev/null &
    fi;
}

function parse_action() {
    if [ -n $action ]; then
        if [ $action = 'change' ]; then
            # pick a new station
            station=$(print -r -l ${(k)stations} | sort | dmenu)
            if [ -n $station ]; then
                play_station $station
            fi

        elif [ $action = 'random' ]; then
            # pick a random station from above
            integer index=$RANDOM%${#${(k)stations}}+1
            station=${${(k)stations}[$index]}
            play_station $station

        elif [ $action = 'last' ]; then 
            # choose the last station
            station=$(cat $LAST_STATION_PATH)
            echo $station
            if [ -n $station ]; then
                if [ ${actions[(r)$station]} ]; then
                    play_station $station
                else
                    echo "Invalid last station found! Populating randomly..."
                    action='random'
                    parse_action $action
                fi
            else 
                echo "No last station found! Populating randomly..."
                action='random'
                parse_action $action
            fi


        elif [ $action = 'stop' ]; then 
            # STAHP
            pkill $PLAYER
        fi 
    fi
}

action=""
get_action $1
parse_action $action
